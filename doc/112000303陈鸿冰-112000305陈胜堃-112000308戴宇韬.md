![image-20230114210759534](C:\Users\chb\AppData\Roaming\Typora\typora-user-images\image-20230114210759534.png)

<br>
<br>
<br>
<br>
<br>
<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>

<br>




# 数字钟设计

## 1. 实验要求

设计一个数字钟，具体设计要求如下：

1. 以时、分、秒形式显示；
2. 二十四小时循环
3. 具有复位功能
4. 有时间校准和调时功能
5. 具有整点报时功能（由点灯闪烁提醒）

## 2. **课题背景**

20世纪末，电子技术获得了飞速的发展。在其推动下，现代电子产品几乎渗透了社会的各个领域，有力地推动了社会生产力的发展和社会信息化程度的提高，同时也使现代电子产品性能进一步提高、产品更新换代的节奏也越来越快。数字钟已成为人们日常生活中必不可少的生活日用品。广泛用于个人家庭以及车站、码头、剧场、办公室等公共场所，给人们的生活、学习、工作、娱乐带来极大的方便。由于数字集成电路技术的发展和采用了先进的石英技术，使数字钟具有走时准确、性能稳定、集成电路有体积小、功耗小、功能多、携带方便等优点。
因此本次设计就用数字集成电路和逻辑门电路来设计一个数字式电子钟，使其完成时间显示校准、调时功能、以及整点报时的功能。

## 3. **设计简介**

在确定选题为“数字钟设计”、以及明确了该选题的各项实验要求后，小组展开了细致的讨论，并查阅了大量的文献资料，最终确定了如下的设计思路：

1. 计时模块：
1. 数码管显示接口：
1. led显示接口：
1. 按键消抖接口：

## 4. **总体方案与分工**

- 模块划分

对于 FPGA，输入为

- 2 个按钮
- 系统时钟与复位

输出为

- 数码管
- led

对于输入和输出，都需要一个接口模块进行数据交互。

时钟主体负责与各个输入输出接口模块进行交互

![总框图](C:/Users/chb/Desktop/digital_clock/doc/pic/总框图.png)

- 分工

||时钟主体|数码管接口|led 接口|按键消抖|音乐播放|
|--|--|--|--|--|--|
|**陈鸿冰**|$\sqrt{}$|$\sqrt{}$|||$\sqrt{}$|
|**陈胜堃**|$\sqrt{}$|||$\sqrt{}$||
|**戴宇韬**|$\sqrt{}$|$\sqrt{}$|$\sqrt{}$|||

## 5. **单元模块**

以下分别设计各个模块

### 5.1 **时钟主体**

时钟主体的输入经过按键消抖后的控制信号，同时需要输出提供给数码管和 led，将输入输出信号按照控制信号和数据信号分类后，可以得到

- 数据通路模块
- 控制通路模块

如上图所示

#### 5.1.1 **时钟主体数据通路**

数据通路负责将输入的信号经过处理，得的需要输出的数据。数据的处理可以按照流水线的方式进行分级，每一级的处理时间为 1 个时钟节拍，也就是输入数据到输出需要需要经过 1 个时钟周期

数据通路需要做的事

1. 存储当前的时间并控制加减
2. 存储闹钟的时间并控制加减
3. 将两者进行对比
4. 输出对比结果给 led模块
5. 输出当前时间或闹钟时间给数码管

要实现这些事，可以构建如下的数据通路

![时钟数据通路](C:/Users/chb/Desktop/digital_clock/doc/pic/时钟数据通路.png)

如图，第一级进行存储当前时间和闹钟时间，并进行控制。包括加一，减一，置数，清零。

第二级进行选择，并采用寄存器输出。同时对比闹钟时间与当前时间，把对比结果用寄存器输出

直接通过数据通路写出代码

```verilog
	alarm inst6_alarm(
		.alarm_keep					(alarm_keep[3]),
		.alarm_add					(alarm_add[3]),
		.alarm_sub					(alarm_sub[3]),
		.alarm_reset				(alarm_reset[19:15]),
		.alarm_clear				(alarm_clear[3]),

		.alarm_numn					(alarm_num6),

		.sysclk						(sysclk),
		.rst_n						(rst_n)
	);

	equal inst6(
		.numn						(num6),
		.alarm_numn					(alarm_num6),

		.eqn						(eq6)
	);

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			l0 <= 1'b0;
		end
		else if (eq6 & eq5 & eq4 & eq3 & eq2 & eq1) begin
			l0 <= 1'b1;
		end
		else if (stop)begin
			l0 <= 1'b0;
		end
	end

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			reg_num6 <= 4'b0;
		end
		else if (tm_sac) begin
			reg_num6 <= num6;
		end
		else begin
			reg_num6 <= alarm_num6;
		end
	end

```

#### 5.1.2 **时钟主体控制模块**

控制模块先考虑输出的控制信号主要实现怎么样的功能。再根据输出的行为确定控制模块的架构

- 计数器架构
- 状态机机构
- 复合结构

![时钟状态机](C:/Users/chb/Desktop/digital_clock/doc/pic/时钟状态机.png)

将信号分类，确定信号由架构的那一部分构成

![时钟输出分类](C:/Users/chb/Desktop/digital_clock/doc/pic/时钟输出分类.png)

![时钟波形](C:/Users/chb/Desktop/digital_clock/doc/pic/时钟波形.png)

根据波形，写出代码。注意先构造出架构，输出的控制信号主要由架构决定，辅助一些中间信号。

```verilog
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			state <= TIMING;
		end
		else begin
			state <= next_state;
		end
	end

	always @ (*) begin
		case (state) 
			TIMING:
				if (TIMING_to_ST6) begin
					next_state = ST6;
				end
				else if (TIMING_to_SAC6) begin
					next_state = SAC6;
				end
				else begin
					next_state = TIMING;
				end
			ST6:
				if (ST6_to_ST5) begin
					next_state = ST5;
				end
				else begin
					next_state = ST6;
				end
			ST5:
				if (ST5_to_ST4) begin
					next_state = ST4;
				end
				else begin
					next_state = ST5;
				end
			ST4:
				if (ST4_to_ST3) begin
					next_state = ST3;
				end
				else begin
					next_state = ST4;
				end
			ST3:
				if (ST3_to_ST2) begin
					next_state = ST2;
				end
				else begin
					next_state = ST3;
				end
			ST2:
				if (ST2_to_ST1) begin
					next_state = ST1;
				end
				else begin
					next_state = ST2;
				end
			ST1:
				if (ST1_to_TIMING) begin
					next_state = TIMING;
				end
				else begin
					next_state = ST1;
				end
			SAC6:
				if (SAC6_to_SAC5) begin
					next_state = SAC5;
				end
				else begin
					next_state = SAC6;
				end
			SAC5:
				if (SAC5_to_SAC4) begin
					next_state = SAC4;
				end
				else begin
					next_state = SAC5;
				end
			SAC4:
				if (SAC4_to_SAC3) begin
					next_state = SAC3;
				end
				else begin
					next_state = SAC4;
				end
			SAC3:
				if (SAC3_to_TIMING) begin
					next_state = TIMING;
				end
				else begin
					next_state = SAC3;
				end
			default:next_state = TIMING;
		endcase
	end

	assign	TIMING_to_ST6	=	long_pression1 & (state == TIMING);
	assign	ST6_to_ST5		=	long_pression1 & (state == ST6);
	assign	ST5_to_ST4		=	long_pression1 & (state == ST5);
	assign	ST4_to_ST3		=	long_pression1 & (state == ST4);
	assign	ST3_to_ST2		=	long_pression1 & (state == ST3);
	assign	ST2_to_ST1		=	long_pression1 & (state == ST2);
	assign	ST1_to_TIMING	=	long_pression1 & (state == ST1);

	assign	TIMING_to_SAC6	=	long_pression0 & (state == TIMING);
	assign	SAC6_to_SAC5	=	long_pression1 & (state == SAC6);
	assign	SAC5_to_SAC4	=	long_pression1 & (state == SAC5);
	assign	SAC4_to_SAC3	=	long_pression1 & (state == SAC4);
	assign	SAC3_to_TIMING	=	long_pression1 & (state == SAC3);

	reg		[25:0]	cnt_seconds;
	wire			cnt_seconds_vld;
	wire			cnt_seconds_end;

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			cnt_seconds <= 0;
		end
		else if (cnt_seconds_end) begin
			cnt_seconds <= 0;
		end
		else if (cnt_seconds_vld) begin
			cnt_seconds <= cnt_seconds + 1'b1;
		end
	end

	assign	cnt_seconds_vld	=	1'b1;
	assign	cnt_seconds_end	=	(cnt_seconds == `SECONDS - 1); //测试的时候改成 50

```
### 5.2 **数码管显示接口**

数码管显示接口要遵守外设接受数据的准则

#### 5.2.1 **数码管显示接口数据通路**

方法同上

1. 将数据的 4 位数据翻译为 8 段数码管识别的数据
2. 将 8 位数据与片选信号拼接，并移位，转换为串行输出

![数码管数据通路](C:/Users/chb/Desktop/digital_clock/doc/pic/数码管数据通信.png)

由数据通路直接写出代码

```verilog
	decoder4_7 inst1_decoder4_7(
		.numn				(num1),

		.vld				(vld[0]),

		.datan				(data1),

		.sysclk				(sysclk),
		.rst_n				(rst_n)
	);

//------------------------------------------------------------

	always @ (*) begin
		if (tw) begin
			data = 7'b111_1111;
		end
		else begin
			case (sel)
				6'b100_000	:	data = data6;
				6'b010_000	:	data = data5;
				6'b001_000	:	data = data4;
				6'b000_100	:	data = data3;
				6'b000_010	:	data = data2;
				6'b000_001	:	data = data1;
				default		:	data = 7'b111_1111;
			endcase
		end
	end

	assign d_p = ~dp;
	assign data_comb = {sel_sd, d_p, data};

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			data_shift <= 14'b000000_1_111111;
		end
		else if (upgrade) begin
			data_shift <= data_comb;
		end
		else if (move) begin
			data_shift <= {data_shift[12:0],data_shift[13]};
		end
		else if (keep) begin
			data_shift <= data_shift;
		end
	end

	assign ds = data_shift[13];
```

#### 5.2.2 **数码管显示接口控制通路**

方法同上，确定架构

![数码架构](C:/Users/chb/Desktop/digital_clock/doc/pic/数码管架构.png)

将控制信号进行分类，并确定由架构的那一部分构成

![数码管输出分类](C:/Users/chb/Desktop/digital_clock/doc/pic/数码管输出分类.png)

绘制主要波形

![数码管波形](C:/Users/chb/Desktop/digital_clock/doc/pic/数码管波形.png)

根据波形，先写搭建的架构，也就是计数器。输出的数据基本以计数为根据，辅助以一些中间信号

```verilog
always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			state1 <= DP6;
		end
		else begin
			state1 <= next_state1;
		end
	end

	always @ (*) begin
		case (state1)
			DP6:
				if (DP6_to_DP5) begin
					next_state1 = DP5;
				end
				else begin
					next_state1 = state1;
				end
			DP5:
				if (DP5_to_DP4) begin
					next_state1 = DP4;
				end
				else begin
					next_state1 = state1;
				end
			DP4:
				if (DP4_to_DP3) begin
					next_state1 = DP3;
				end
				else begin
					next_state1 = state1;
				end
			DP3:
				if (DP3_to_DP2) begin
					next_state1 = DP2;
				end
				else begin
					next_state1 = state1;
				end
			DP2:
				if (DP2_to_DP1) begin
					next_state1 = DP1;
				end
				else begin
					next_state1 = state1;
				end
			DP1:
				if (DP1_to_DP6) begin
					next_state1 = DP6;
				end
				else begin
					next_state1 = state1;
				end
			default	: next_state1 = DP6;
		endcase
	end

	assign DP6_to_DP5 = cnt_595_end & (cnt_14_end)& (state1 == DP6);
	assign DP5_to_DP4 = cnt_595_end & (cnt_14_end)& (state1 == DP5);
	assign DP4_to_DP3 = cnt_595_end & (cnt_14_end)& (state1 == DP4);
	assign DP3_to_DP2 = cnt_595_end & (cnt_14_end)& (state1 == DP3);
	assign DP2_to_DP1 = cnt_595_end & (cnt_14_end)& (state1 == DP2);
	assign DP1_to_DP6 = cnt_595_end & (cnt_14_end)& (state1 == DP1); 

```

### 5.3 **led 显示接口**

由单一控制模块构成

输入的控制信号有

- 闪烁频率 ：twinkle_led
- 选择闪烁 led ：twinkle_led

需要根据输入的闪烁频率，确定计数的终值。再根据终值的为分界线，将 led 点亮信号的占空比设置为 0.5

主要代码如下

```verilog
	always @ (*) begin
		case (twinkle_led)
			10'b00000_00001: //0.2s
				x = 10_000_000;
			10'b00000_00010: //0.5s
				x = 25_000_000;
			10'b00000_00100: //1.0s
				x = 50_000_000;
			10'b00000_01000: //1.5s
				x = 75_000_000;
			10'b00000_10000: //2.0s
				x = 100_000_000;
			10'b00001_00000: //2.5s
				x = 125_000_000;
			10'b00010_00000: //3.0s
				x = 150_000_000;
			10'b00100_00000: //4.0s
				x = 200_000_000;
			10'b01000_00000: //5.0s
				x = 250_000_000;
			10'b10000_00000: //6.0s
				x = 300_000_000;
			default: //1.0s
				x = 50_000_000;
		endcase
	end

	reg		[28:0]	cnt;
	wire			cnt_vld;
	wire			cnt_end;

	//
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			cnt <= 0;
		end
		else if (cnt_end) begin
			cnt <= 0;
		end
		else if (cnt_vld) begin
			cnt <= cnt + 1'b1;
		end
	end

	assign	cnt_vld	=	1'b1;
	assign	cnt_end =	(cnt == x-1);

	wire			twinkle;

	assign	twinkle = (cnt >= 0 & cnt < x/2) ? 1'b1 : 1'b0;

	genvar i;
	generate
		for (i=9; i>=0; i=i-1) begin : label
			tw inst_tw(
				.lin				(lin[i]),
				.valid_led			(valid_led[i]),
				.twinkle_led		(twinkle_led[i]),
				.twinkle			(twinkle),

				.lout				(lout[i]),

				.sysclk				(sysclk),
				.rst_n				(rst_n)
			);
		end
	endgenerate

	assign	led9 = lout[9];
	assign	led8 = lout[8];
	assign	led7 = lout[7];
	assign	led6 = lout[6];
	assign	led5 = lout[5];
	assign	led4 = lout[4];
	assign	led3 = lout[3];
	assign	led2 = lout[2];
	assign	led1 = lout[1];
	assign	led0 = lout[0];
```

### 5.4 **按键消抖接口**

由两个控制模块构成，第一个模块负责将输入流的信号平滑化。第二个模块将平滑后信号的持续时间分为长按与短按

#### 5.4.1 **按键平滑模块**

采用移位寄存器的方式判断输入信号时候稳定，设寄存器的输入个数为 19 个，只要连续输入 19 个 0，就认为输入的信号已经平稳

代码如下

```verilog
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			key_reg <= 19'h7ffff;
		end
		else begin
			key_reg <= {key_reg[17:0], key};
		end
	end

	assign key_mooth = &(~(key_reg));
```

#### 5.4.2 **长短按判断模块**

以计数器作为架构，绘制出主要的波形图

![长短按](C:/Users/chb/Desktop/digital_clock/doc/pic/长短按.png)

主要代码如下

```verilog
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			key_mooth_r1 <= 1'b0;
		end
		else begin
			key_mooth_r1 <= key_mooth;
		end
	end

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			cnt <= 26'b0;
		end
		else if (cnt_end) begin
			cnt <= 26'b0;
		end
		else if (cnt_vld) begin
			cnt <= cnt + 1'b1;
		end
	end

	assign cnt_vld = key_mooth & (cnt < `LONG_PRESSION - 1);
	assign cnt_end = (~key_mooth & key_mooth_r1);

//-------------------------------------------------------

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			short_pression <= 1'b0;
		end
		else if (short_pression) begin
			short_pression <= 1'b0;
		end
		else if ((cnt>=`SHORT_PRESSION-1) & (cnt<`LONG_PRESSION-1) & cnt_end) begin
			short_pression <= 1'b1;
		end
	end

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			long_pression <= 1'b0;
		end
		else if (long_pression) begin
			long_pression <= 1'b0;
		end
		else if ((cnt==`LONG_PRESSION-1) & cnt_end) begin
			long_pression <= 1'b1;
		end
	end
endmodule

```

### 5.5 **音乐播放代码**

#### 5.1 总体连接

![image-20230114211010862](C:\Users\chb\AppData\Roaming\Typora\typora-user-images\image-20230114211010862.png)

#### 5.2 控制波形

![image-20230114211109340](C:\Users\chb\AppData\Roaming\Typora\typora-user-images\image-20230114211109340.png)

#### 5.3 实现代码

```verilog
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			start_r1 <= `MSC_N'b0;
		end
		else begin
			start_r1 <= start;
		end
	end

	assign	begins	=	(start_r1 != start) & (start != 0);
	assign	ends	=	(start_r1 != start) & (start == 0);

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			state <= IDLE;
		end
		else begin
			state <= next_state;
		end
	end

	always @ (*) begin
		case (state)
			IDLE:
				if (idle_to_inst) begin
					next_state = INST;
				end
				else begin
					next_state = state;
				end
			INST:
				if (inst_to_send_ad) begin
					next_state = SEND_AD;
				end
				else if (inst_to_idle) begin
					next_state = IDLE;
				end
				else begin
					next_state = state;
				end
			SEND_AD:
				if (send_ad_to_inst) begin
					next_state = INST;
				end
				else if (send_ad_to_idle) begin
					next_state = IDLE;
				end
				else begin
					next_state = state;
				end
			default:
				next_state = IDLE;
		endcase
	end

	assign	idle_to_inst	=	(state == IDLE) & (begins);
	assign	inst_to_send_ad	=	(state == INST) & (cnt_end);
	assign	inst_to_idle	=	(state == INST) & (ends);
	assign	send_ad_to_inst	=	(state == SEND_AD) & (begins | q == 6'b111000);
	assign	send_ad_to_idle	=	(state == SEND_AD) & (ends);
	
	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			cnt <= 25'b0;
		end
		else if (cnt_end) begin
			cnt <= 25'b0;
		end
		else if (cnt_vld) begin
			cnt <= cnt + 1'b1;
		end
	end

	assign	cnt_vld	=	state != IDLE;
	assign	cnt_end	=	cnt_vld & (state == INST & cnt == 5) | ((state == SEND_AD) & (cnt >= (60*50_000_000/(4*q_cb)) - 1)) | ends;

	always @ (posedge sysclk or negedge rst_n) begin
		if (~rst_n) begin
			q_cb <= 12'b0;
		end
		else if (state == INST & cnt == 2) begin
			q_cb <= {6'b000000,q};
		end
		else if (state == INST &  cnt == 4) begin
			q_cb <= {q_cb[5:0],q};
		end
		else if (ends) begin
			q_cb <= 12'b0;
		end
	end

//------------------------------------------------------------------

	assign	tran_vld	=	(state == SEND_AD & cnt_end) & ~ends;
	assign	tran_end	=	ends;

//-------------------------------------------------------------------

	assign	rom_clock		=	(state == INST & (cnt == 2 | cnt == 4)) 
							| (state == SEND_AD & cnt_end);

//-------------------------------------------------------------------

	assign	add			=	(state == INST & cnt == 2)
							| (state == SEND_AD & cnt >= 60*50_000_000/(4*q_cb) - 3 & cnt < 60*50_000_000/(4*q_cb) - 2);

	always @ (*) begin
		if (ends) begin
			sel = `MSC_N'b1;
		end
		else if (state == INST & cnt == 0) begin
			sel = start;
		end
		else begin
			sel = `MSC_N'b0;
		end
	end

	assign volume_out = volume_in;
```

### 5.6 **总体连接**

![总体连接](C:/Users/chb/Desktop/digital_clock/doc/pic/总体连接.png)

## 6. **仿真结果**

通过 quartus2 与 modelsim 联合仿真

### 6.1 **时钟主体仿真**

![clock](C:\Users\chb\Desktop\digital_clock\doc\pic\wave\clock\clock.png)

![clock2](C:\Users\chb\Desktop\digital_clock\doc\pic\wave\clock\clock2.png)

### 6.2 **数码管显示接口仿真**

![sd_itf](C:\Users\chb\Desktop\digital_clock\doc\pic\wave\sd_itf\sd_itf.png)

![sd_itf2](C:\Users\chb\Desktop\digital_clock\doc\pic\wave\sd_itf\sd_itf2.png)

### 6.3 **按键消抖仿真**

![pb_itf_0](C:\Users\chb\Desktop\digital_clock\doc\pic\wave\pb_itf\pb_itf_0.png)

## 7. **上板测试**



![图片1](C:\Users\chb\Desktop\digital_clock\doc\pic\图片1.png)

![图片2](C:\Users\chb\Desktop\digital_clock\doc\pic\图片2.png)

![图片3](C:\Users\chb\Desktop\digital_clock\doc\pic\图片3.png)



## 8. **实验心得**



1. 根据实现的功能进行划分模块，注意模块间低耦合，模块内高内聚。可以有效地提升模块的复用性与可拓展性
2. 分析每一个模块的类型，可以吧模块分成数据通路 和 控制通路，两者都包含的就是 复合通路。分析的时候先确定模块的输入输出有哪些，以控制和数据为标准将输入输出分类
3. 先数据后控制，先确定输入的数据需要经过怎么样的处理才可以得出输出数据，数据的输入到输出需要哪些辅助的控制信号，数据的输入到输出需要几个时钟周期，可以采用流水线的方式构造数据通路
4. 根据数据通路中确定的控制信号，以及数据通路的输出信号，对它们的行为变化进行分析。从而确定大致架构。如状态机，计数器。输出信号以架构为根，辅助一些中间型号进行控制
5. 数据通路的框图决定数据通路的代码。控制通路的波形决定控制通路的代码
6. 通过modelsim仿真。查看关键信号时候正确，尤其是架构。
